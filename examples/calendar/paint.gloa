--[[============================================================
--=
--=  Paint functionality
--=
--=-------------------------------------------------------------
--=
--=  Example program: Calendar
--=  by Marcus 'ReFreezed' ThunstrÃ¶m
--=
--============================================================]]

local theBrush: wxBrush  = NULL
local thePen:   wxPen    = NULL
local theFont:  wxFont   = NULL
local theColor: wxColour = NULL

export preparePaint :: () {
	theBrush = wxBrush(wxBLACK, wxSOLID)
	thePen   = wxPen(wxBLACK, 1, wxTRANSPARENT)
	theFont  = wxFont(11, wxFONTFAMILY_DEFAULT, wxFONTSTYLE_NORMAL, wxFONTWEIGHT_NORMAL)
	theColor = wxColour(wxBLACK)
}

export setColor :: (r,g,b:float) {
	local ir = round(r*255)
	local ig = round(g*255)
	local ib = round(b*255)
	theColor.Set!(ir, ig, ib)
	theBrush.SetColour!(theColor)
	thePen.SetColour!(theColor)
}
export setColor :: (grey:float) {
	setColor(grey, grey, grey)
}
export setColor :: (color:wxColour) {
	theColor.Set!(color.Red!(), color.Green!(), color.Blue!())
	theBrush.SetColour!(theColor)
	thePen.SetColour!(theColor)
}

export getTextDimensions :: (dc:wxPaintDC, text:string) -> (w,h:int) {
	local w, h = dc.GetTextExtent!(text, theFont)
	return w, h
}
export setFont :: (dc:wxPaintDC, size:int, style=wxFONTSTYLE_NORMAL, weight=wxFONTWEIGHT_NORMAL) {
	theFont.SetPointSize!(size)
	theFont.SetStyle!(style)
	theFont.SetWeight!(weight)
	dc.SetFont!(theFont)
}

local updateBrushAndPen :: (dc:wxPaintDC, brushStyle,penStyle:int) {
	theBrush.SetStyle!(brushStyle)
	thePen.SetStyle!(penStyle)
	dc.SetBrush!(theBrush)
	dc.SetPen!(thePen)
}

export drawRectangleFill :: (dc:wxPaintDC, x,y,w,h:float) {
	updateBrushAndPen(dc, wxSOLID, wxTRANSPARENT)
	dc.DrawRectangle!(round(x), round(y), round(w), round(h))
}
export drawRectangleLine :: (dc:wxPaintDC, x,y,w,h:float) {
	updateBrushAndPen(dc, wxTRANSPARENT, wxSOLID)
	dc.DrawRectangle!(round(x), round(y), round(w), round(h))
}

export drawText :: (dc:wxPaintDC, text:string, x,y:float) {
	dc.SetTextForeground!(theColor)
	dc.DrawText!(text, round(x), round(y))
}
