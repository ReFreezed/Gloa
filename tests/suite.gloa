--
-- GlÃ³a test suite
--
-- Program arguments:
-- * testToRun (optional, default is all)
--

!import "basic"
!import "compiler"
!import "io"
!import "os"
!import "string"
!import "utils"

!load "suite.fails"

local TEST_FILES :: {
	"examples/lovelyBlaster/main.gloa",
	"examples/lovelySnake/main.gloa",
	"tests/basic.gloa",
	"tests/compileTimeExecution.gloa",
	"tests/deepCompilation.gloa",
	"tests/modules.gloa",
	"tests/types.gloa",
}

local COMPILER_OPTIONS   :: "--debug --nogc --silent"
local OUTPUT_REDIRECTION :: ">NUL" -- ">NUL 2>&1"

!run {
	local options      = getBuildOptions()
	options.outputType = BuildOutputType.NONE
	setBuildOptions(options)

	disableBuffering(STDOUT)
	disableBuffering(STDERR)

	local testToRun = getProgramArguments()[1]
	testToRun       = testToRun ~= NULL ? toLower(testToRun) : ""

	if testToRun  printf("Looking for test '%s'.", testToRun)

	local testCount = 0
	local failCount = 0
	local lines: []string

	local shouldRunTest = [testToRun] (path:string) -> bool {
		if not testToRun  return true
		local _, basename = matchPattern(path, "([^/]+)%.gloa$")
		return (find(toLower(basename), testToRun))
	}

	for path: TEST_FILES {
		if shouldRunTest(path) {
			testCount += 1
			local cmd   = format([["lua "%s" %s "%s" %s"]], COMPILER_PATH, COMPILER_OPTIONS, path, OUTPUT_REDIRECTION)

			print(cmd)
			if execute(cmd) == 0 {
				insert(lines, format("ok      %s", path))
			} else {
				insert(lines, format("NOT_OK  %s", path))
				failCount = failCount+1
			}

			if testToRun ~= ""  break
		}
	}

	local FILES_TO_COPY_TO_TEMP_FOLDER :: {
		"tests/types.errorGlobal.gloa",
		"tests/types.errorLocal.gloa",
		"tests/types.load.gloa",
	}

	for path: FILES_TO_COPY_TO_TEMP_FOLDER {
		local ok, filename = matchPattern(path, "[^/]+$")
		assert(ok, path)

		local tempPath = "local/temp/" .. cast(string) filename
		copy(path, tempPath)
	}

	for gloa, i: TESTS_THAT_SHOULD_FAIL {
		local tempPath = format("local/temp/shouldFail%d.gloa", i)

		if shouldRunTest(tempPath) {
			testCount += 1

			if not find(gloa, "main ::")
				gloa = gloa.."\nlocal main :: () {}\n"

			local ok, err = writeEntireFile(tempPath, gloa)
			assert(ok, err)

			local cmd = format([["lua "%s" %s "%s" %s"]], COMPILER_PATH, COMPILER_OPTIONS, tempPath, OUTPUT_REDIRECTION)
			print(cmd)

			if execute(cmd) ~= 0 {
				insert(lines, format("ok      %s", tempPath))
			} else {
				insert(lines, format("NOT_OK  %s", tempPath))
				failCount = failCount+1
			}

			if testToRun ~= ""  break
		}
	}

	print("-"*64)
	for lines  print(it)
	print("-"*64)

	if testCount == 0
		print("No tests ran!")
	elseif failCount == 0
		printf("Completed all %d tests successfully. Yay!", testCount)
	else
		printf("Failed %d of %d", failCount, testCount)

	print()
}
